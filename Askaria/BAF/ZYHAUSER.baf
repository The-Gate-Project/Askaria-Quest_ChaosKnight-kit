IF
  !See([ENEMY])
  See([PC])
  NumberOfTimesTalkedTo(0)
  !StateCheck([PC],STATE_INVISIBLE)
  !StateCheck([PC],STATE_IMPROVEDINVISIBILITY)
THEN
  RESPONSE #100
    StartDialogue("zyhauser",[PC])
END

IF
  See([PC])
  Global("HAU#SM","GLOBAL",1)
THEN
  RESPONSE #100
    SmallWait(2)
    StartDialogue("zyhauser",[PC])
    Continue() //<-정말 중요한 구문이다. 이게 없으면 계속 밑에 IF구문을 실행하려고 버벅대면서 스크립트대로 전투를 하지 않는다. 하지만 이것을 추가함으로써 자연스러운 전투전개가 이루어지며, 결국 밑의 IF구문의 조건에 해당하면 PC와 대화하게된다.
END

IF
  HPPercentLT(Myself,30)
  Global("ZyhauserAlmostDie","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("ZyhauserAlmostDie","GLOBAL",1)
    Dialogue([PC])
END




IF
  See(NearestEnemyOf(Myself))
  HPPercentLT(Myself,25)
  HPGT(Myself,1)
  !Range(NearestMyGroupOfType,10)
  !GlobalTimerNotExpired("ZYWhined","LOCALS")
THEN
  RESPONSE #50
    SetGlobalTimer("ZYWhined","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46151)
    Shout(ASSIST)
    Continue()
  RESPONSE #50
    SetGlobalTimer("ZYWhined","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,49763)
    Shout(ASSIST)
    Continue()
  RESPONSE #50
    SetGlobalTimer("ZYWhined","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,49764)
    Shout(ASSIST)
    Continue()
  RESPONSE #50
    SetGlobalTimer("ZYWhined","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,49765)
    Shout(ASSIST)
    Continue()
END

IF
  BreakingPoint()
THEN
  RESPONSE #50
    DisplayStringHead(Myself,49766)
    Shout(ASSIST)
  RESPONSE #50
    DisplayStringHead(Myself,49767)
    Shout(ASSIST)
  RESPONSE #50
    DisplayStringHead(Myself,49768)
    Shout(ASSIST)
END

IF
  HPPercentLT(Myself,20)
  Global("ZYSecondWind","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ZYSecondWind","LOCALS",1)
    DisplayStringHead(Myself,73944)
    ApplySpell(Myself,CLERIC_HEAL)
END

IF
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("ZYDrankPotioTime","LOCALS")
  HasItem("POTN55",Myself)
THEN
  RESPONSE #100
    SetGlobalTimer("ZYDrankPotioTime","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46150)
    UseItem("POTN55",Myself)
END

IF
  See(NearestEnemyOf(Myself))
  OR(2)
    Range(NearestMyGroupOfType,20)
    See(NearestMyGroupOfType([0.GENERAL_ITEM.0.CLERIC_ALL]))
  HPPercentLT(Myself,35)
  !GlobalTimerNotExpired("ZYCallForHeal","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("ZYCallForHeal","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,72808)
    Shout(HEALME)
    Continue()
END

IF
  HitBy([0],CRUSHING)
  Global("ZYGiantStrengPotio","LOCALS",0)
  !GlobalTimerNotExpired("ZYDrankPotioTime","LOCALS")
  HasItem("POTN05",Myself)
THEN
  RESPONSE #100
    SetGlobalTimer("ZYDrankPotioTime","LOCALS",ONE_ROUND)
    SetGlobal("ZYGiantStrengPotio","LOCALS",1)
    DisplayStringHead(Myself,46150)
    UseItem("POTN05",Myself)
END

IF
  HPPercentLT(Myself,50)
  !Range(NearestEnemyOf(Myself),5)
  HaveSpell(MONK_LAY_ON_HANDS)
  !GlobalTimerNotExpired("ZYLayOnHands","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("ZYLayOnHands","LOCALS",ONE_ROUND)
    Spell(Myself,MONK_LAY_ON_HANDS)
END

IF
  HPPercentLT(Myself,50)
  OR(2)
    HaveSpell(MONK_LAY_ON_HANDS)
    HasItem("POTN55",Myself)
  Range(NearestEnemyOf(Myself),6)
  Range(SecondNearestEnemyOf(Myself),6)
  RandomNum(2,1)
  Global("ZYTimeToMove","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ZYTimeToMove","LOCALS",1)
    Continue()
END

IF
  RandomNum(2,1)
  AttackedBy([GOODCUTOFF],DEFAULT)
  Range(NearestEnemyOf(Myself),8)
  Range(SecondNearestEnemyOf(Myself),8)
  Range(ThirdNearestEnemyOf(Myself),8)
  Global("ZYTimeToMove","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ZYTimeToMove","LOCALS",1)
    Continue()
END

IF
  RandomNum(3,1)
  HPPercentLT(Myself,30)
  OR(2)
    HaveSpell(MONK_LAY_ON_HANDS)
    HasItem("POTN55",Myself)
  Range(NearestEnemyOf(Myself),6)
  Global("ZYTimeToMove","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ZYTimeToMove","LOCALS",1)
    Continue()
END


IF
  RandomNum(3,1)
  HPPercentLT(Myself,75)
  Range(LastSeenBy(Myself),10)
  Global("ZYLunar","GLOBAL",0)
  !StateCheck(Myself,STATE_INVISIBLE)
  !StateCheck(Myself,STATE_NORMAL)
  !GlobalTimerNotExpired("ZYShadowStance","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("ZYShadowStance","LOCALS",FIVE_ROUNDS)
    DisplayStringHead(Myself,73943)
    ReallyForceSpellRES("BALTH10",Myself)
END

IF
  !GlobalTimerNotExpired("ZYMonkTimer","LOCALS")
  Range(LastSeenBy(Myself),4)
  RandomNum(3,1)
  Global("ZYLunar","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobalTimer("ZYMonkTimer","LOCALS",3)
    DisplayStringHead(Myself,73942)
    FaceObject(LastSeenBy(Myself))
    SwingOnce()
    PlaySound("BALTHA02")
    ReallyForceSpellRES("BALTH09",LastSeenBy(Myself))
END

IF
  !GlobalTimerNotExpired("ZYMonkTimer","LOCALS")
  Range(LastSeenBy(Myself),10)
  RandomNum(2,1)
  Global("ZYLunar","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobalTimer("ZYMonkTimer","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,72915)
    ForceSpellRES("BALTH03",Myself)
    Continue()
END

IF
  !GlobalTimerNotExpired("ZYMonkTimer","LOCALS")
  Range(LastSeenBy(Myself),10)
  RandomNum(2,2)
  Global("ZYLunar","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobalTimer("ZYMonkTimer","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,72916)
    ForceSpellRES("BALTH04",Myself)
    Continue()
END

IF
  !GlobalTimerNotExpired("ZYMonkTimer","LOCALS")
  Range(LastSeenBy(Myself),5)
  RandomNum(2,1)
  Global("ZYLunar","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobalTimer("ZYMonkTimer","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,71989)
    ForceSpellRES("BALTH05",Myself)
    Continue()
END

IF
  !GlobalTimerNotExpired("ZYMonkTimer","LOCALS")
  HaveSpell(MONK_QUIVERING_PALM)
  RandomNum(2,2)
  Global("ZYLunar","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobalTimer("ZYMonkTimer","LOCALS",5)
    DisplayStringHead(Myself,72936)
    ForceSpell(Myself,MONK_QUIVERING_PALM)
    Continue()
END

IF
  !GlobalTimerNotExpired("ZYMonkTimer","LOCALS")
  HaveSpell(MONK_STUNNING_BLOW)
  RandomNum(2,1)
  Global("ZYLunar","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobalTimer("ZYMonkTimer","LOCALS",9)
    DisplayStringHead(Myself,72937)
    ForceSpell(Myself,MONK_STUNNING_BLOW)
    Continue()
END

IF
  !HPPercentLT(Myself,20)
  Global("ZYLunar","GLOBAL",0)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),15)
END

IF
  HPPercentLT(Myself,20)
  OR(3)
    HasItem("POTN55",Myself)
    HaveSpell(MONK_LAY_ON_HANDS)
    Range(NearestMyGroupOfType,20)
  Global("ZYLunar","GLOBAL",0)
THEN
  RESPONSE #100
    RunAwayFrom(NearestEnemyOf(Myself),45)
END

IF
  HPPercentLT(Myself,20)
  !HasItem("POTN55",Myself)
  !HaveSpell(MONK_LAY_ON_HANDS)
  !Range(NearestMyGroupOfType,20)
  Global("ZYLunar","GLOBAL",0)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),15)
END

