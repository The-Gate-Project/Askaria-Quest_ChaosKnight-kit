IF
	Die()
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,ILLUSION_DEATH)  // SPIN835.SPL (No such index)
		Wait(1)
		DestroySelf()
END

IF
	Detect([PC])
	Global("ZYjoDisplay","GLOBAL",0)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%Wediyer_JOstreff%)  // @96631 (EET) @57522 (BGT)  @31819 (BGEE)
		SetGlobal("ZYjoDisplay","GLOBAL",1)
		AttackReevaluate(NearestEnemyOf(Myself),10)
END

IF
	See(NearestEnemyOf(Myself))
	Delay(15)
THEN
	RESPONSE #100
		ApplySpell(Myself,CLERIC_PROTECT_FROM_EVIL)  // SPPR107.SPL (Protection From Evil)
		Continue()
END

IF
	HPPercentLT(Myself,30)
	Global("ZYjoCOURAGE","GLOBAL",0)
THEN
  RESPONSE #100
		CreateCreatureObject("CAVENE",Myself,0,0,0)
		DisplayStringHead(Myself,%Wediyer_JOstreff3%)  // @96631 (EET) @57522 (BGT)  @31819 (BGEE)
		ApplySpell(Myself,INVOKE_COURAGE)
		ApplySpell(Myself,CLERIC_REMOVE_FEAR)
		SetGlobal("ZYjoCOURAGE","GLOBAL",1)
END

IF
	HPPercentLT(Myself,50)
	Global("ZYjoDisplay","GLOBAL",1)
THEN
	RESPONSE #100
		CreateCreatureObject("WOLFWI",Myself,0,0,0)
		DisplayStringHead(Myself,%Wediyer_JOstreff2%)  // @96631 (EET) @57522 (BGT)  @31819 (BGEE)
		SetGlobal("ZYjoDisplay","GLOBAL",2)
END

IF
	Detect(NearestEnemyOf(Myself))
	!See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),40)
	HaveSpell(INQUIS_TRUE_SIGHT)  // SPCL232.SPL (True Sight)
THEN
	RESPONSE #100
		Spell(Myself,INQUIS_TRUE_SIGHT)  // SPCL232.SPL (True Sight)
		Continue()
END

IF
	HPPercentLT(Myself,50)
	HaveSpell(PALADIN_LAY_ON_HANDS)  // SPCL211.SPL (Lay On Hands)
THEN
	RESPONSE #100
		Spell(Myself,PALADIN_LAY_ON_HANDS)  // SPCL211.SPL (Lay On Hands)
		Continue()
END

IF
	See(NearestEnemyOf(Myself))
	OR(6)
		StateCheck(LastSeenBy(Myself),STATE_HASTED)
		StateCheck(LastSeenBy(Myself),STATE_BLESS)
		StateCheck(LastSeenBy(Myself),STATE_CHANT)
		StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
		StateCheck(LastSeenBy(Myself),STATE_BLUR)
		HasImmunityEffects(LastSeenBy(Myself))
	    HaveSpell(INQUIS_DISPEL_MAGIC)  // SPCL231.SPL (Dispel Magic)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),INQUIS_DISPEL_MAGIC)  // SPCL231.SPL (Dispel Magic)
		Continue()
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate(LastSeenBy(Myself),30)
		Continue()
END

IF
	AttackedBy([ANYONE],DEFAULT)
THEN
	RESPONSE #100
		Attack(LastAttackerOf(Myself))
		Continue()
END

