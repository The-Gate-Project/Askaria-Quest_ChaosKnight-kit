BACKUP ~ChaosKnight/backup~
AUTHOR ~drizztdd@naver.com~
README ~Askaria/Readmes/Readme.%LANGUAGE%.txt~ ~Askaria/Readmes/readme.English.txt~

VERSION ~v2.1~

ALWAYS

ACTION_IF (GAME_IS ~bgee eet bgt~) BEGIN
INCLUDE ~ChaosKnight/Lib/g3_cpmvars_master.tpa~
END

INCLUDE ~ChaosKnight/lib/a7#add_kit_ex.tpa~

ACTION_IF (GAME_IS ~bg2ee iwdee~) BEGIN
PRINT @201 /*Only the ChaosKnight Kit will be installed on BG2EE / IWDEE*/
PRINT ~   ~
END

    //Copies tra files into the autotra-folder (to leave the originals untouched)
    DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
	  COPY ~ChaosKnight/Language/english/D~  ~ChaosKnight/Language/autotra/%LANGUAGE%/D~
      COPY ~ChaosKnight/Language/english~  ~ChaosKnight/Language/autotra/%LANGUAGE%~
	  COPY ~ChaosKnight/Language/%LANGUAGE%/D~  ~ChaosKnight/Language/autotra/%LANGUAGE%/D~
      COPY ~ChaosKnight/Language/%LANGUAGE%~  ~ChaosKnight/Language/autotra/%LANGUAGE%~
    END

    LAF autotra_workaround END


//  ACTION_DEFINE_ARRAY fl#noconvert BEGIN END

 ACTION_DEFINE_ARRAY fl#reload BEGIN setup END

    LAF HANDLE_CHARSETS
      INT_VAR
        infer_charsets = 1
      STR_VAR
        tra_path = EVAL ~ChaosKnight/Language/autotra~
//        noconvert_array = fl#noconvert
        reload_array = fl#reload
 END
END

AUTO_TRA ~ChaosKnight/Language/autotra/%LANGUAGE%/D~

LANGUAGE ~Korean~ ~korean~ ~ChaosKnight/Language/korean/setup.tra~
LANGUAGE ~English~ ~english~ ~ChaosKnight/Language/english/setup.tra~
LANGUAGE ~French~ ~french~ ~ChaosKnight/Language/french/setup.tra~

BEGIN @0 /* Install the Chaos Knight Kit for BGT, BGEE, BG2EE, IWDEE or EET and Jacob, Npc pour BGT, BGEE ou EET */
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet bgt iwdee~) @200 /*ChaosKnight must be installed on BGT, BGEE, BG2EE, IWDEE or EET*/

COPY ~ChaosKnight/SPL/zycs1.spl~ ~override~
   SAY NAME1 @8

COPY ~ChaosKnight/SPL/zycs2.spl~ ~override~
   SAY NAME1 @9

COPY ~ChaosKnight/SPL/zycs3.spl~ ~override~
   SAY NAME1 @10

COPY ~ChaosKnight/SPL/zycs4.spl~ ~override~
   SAY NAME1 @11

COPY ~ChaosKnight/SPL/zycs5.spl~ ~override~
   SAY NAME1 @12

COPY ~ChaosKnight/SPL/zycs6.spl~ ~override~
   SAY NAME1 @13

COPY ~ChaosKnight/SPL/zycs7.spl~ ~override~
   SAY NAME1 @14

COPY ~ChaosKnight/SPL/zycs8.spl~ ~override~
   SAY NAME1 @15

COPY ~ChaosKnight/SPL/zycs8_1.spl~ ~override~

COPY ~ChaosKnight/EFF/zychaosd.eff~ ~override~

COPY ~ChaosKnight/EFF/zychaost.eff~ ~override~


/* ---------------------------------------------------- *
 *  Copy BAMs or convert them to EE  *
 * ---------------------------------------------------- */
 

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
	INCLUDE ~%MOD_FOLDER%/lib/gw_convert_inventory_bam_to_ee.tpa~			// Loads function converting inventory BAMs to EE.
	//OUTER_SPRINT file-exc ~ NAME of Bam in FOLDER to EXCLUDE~	// Special cases: unused inventory or description icons, and ability .bam don't need to be converted.
	PRINT ~Convert BAMs to EE~
	ACTION_BASH_FOR ~ChaosKnight/BAM/~ ~^.+\.bam$~ BEGIN
		ACTION_TO_LOWER %BASH_FOR_FILE%
		ACTION_TO_LOWER %BASH_FOR_RES%
		ACTION_IF (~%file-exc%~ STRING_CONTAINS_REGEXP ~%BASH_FOR_RES%~ = 1) BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
				LPF ~gw_convert_inventory_bam_to_ee~ END
			BUT_ONLY
		END ELSE BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
		END
	END
END ELSE BEGIN  // If game not EE add Bam without change
	COPY ~ChaosKnight/BAM~ ~override~
END


// Item

// Item can be equipped by one and unique NPC using ITEM_USE.2DA ... (https://gibberlings3.github.io/iesdp/files/2da/2da_bgee/item_use.htm)

// zychmace = Item ressource / ZYJCOB = Script name of npc / %use_it% = Message in inventory when another pnj attempts to equip the item / 2 = conditions that display the message in the inventory / %use_him% = Name of NPC (If existing NPC use streff reference in game)

// See below nuances for Tobex version ! (And look at ITEM_USE.2DA in Near Infinity)

ACTION_IF (GAME_IS ~bgee eet~) BEGIN
PRINT ~EE item description~
PRINT ~   ~
PRINT ~Item restricted to Jacob~
PRINT ~   ~
COPY ~ChaosKnight/ITM/zychmace.itm~ ~override~
  SAY NAME1 @18
  SAY NAME2 @6
  SAY UNIDENTIFIED_DESC @16 
  SAY DESC @17
  WRITE_ASCII 0x3A ~zychmace~ (8)    // Write bam to item (second offset Bam for use (displayed in action bar)) 
  WRITE_ASCII 0x76 ~zychmace~ (8)    // Write bam to item (second offset Bam for use (displayed in action bar)) 
  
OUTER_SET use_him = RESOLVE_STR_REF (@4)
OUTER_SET use_it = RESOLVE_STR_REF (@203)
APPEND ~ITEM_USE.2DA~ ~zychmace   ZYJCOB      %use_it%      2          %use_him%~ UNLESS ~zychmace~ 
	
END ELSE BEGIN 

ACTION_IF (GAME_IS ~bgt~ AND MOD_IS_INSTALLED ~SETUP-TOBEX.TP2~ ~100~) BEGIN
PRINT ~Bgt item description~
PRINT ~   ~
PRINT ~Tobex is Installed~
PRINT ~   ~
PRINT ~Item restricted to Jacob~
PRINT ~   ~
COPY ~ChaosKnight/ITM/zychmace.itm~ ~override~
  SAY NAME1 @18
  SAY NAME2 @6
  SAY UNIDENTIFIED_DESC @16 
  SAY DESC @7
  WRITE_ASCII 0x3A ~zychmace~ (8)    // Write bam to item (second offset Bam for use (displayed in action bar)) 
  WRITE_ASCII 0x76 ~zychmace~ (8)    // Write bam to item (second offset Bam for use (displayed in action bar)) 
OUTER_SET use_it = RESOLVE_STR_REF (@203)
APPEND ~ITEM_USE.2DA~ ~zychmace   ZYJCOB      -1      2          %use_it%~ UNLESS ~zychmace~
	
END ELSE BEGIN 

ACTION_IF (GAME_IS ~bgt~ AND NOT MOD_IS_INSTALLED ~SETUP-TOBEX.TP2~ ~100~) BEGIN
PRINT ~Tobex is not Installed~
PRINT ~zychmace.itm restriction need Tobex for BGT~
PRINT ~zychmace.itm will not be restricted to Jacob only~
PRINT ~   ~
COPY ~ChaosKnight/ITM/zychmace.itm~ ~override~
  SAY NAME1 @18
  SAY NAME2 @6
  SAY UNIDENTIFIED_DESC @16 
  SAY DESC @7
  WRITE_ASCII 0x3A ~zychmace~ (8)    // Write bam to item (second offset Bam for use (displayed in action bar)) 
  WRITE_ASCII 0x76 ~zychmace~ (8)    // Write bam to item (second offset Bam for use (displayed in action bar)) 

  END
 END
END

// Portraits

ACTION_IF (GAME_IS ~bgee eet~) BEGIN
PRINT ~EE Portraits~
PRINT ~   ~
  COPY ~ChaosKnight/BMP/EE/zychaosL.bmp~ ~override~
  COPY ~ChaosKnight/BMP/EE/zychaosS.bmp~ ~override~
  COPY ~ChaosKnight/BMP/EE/zychaosM.bmp~ ~override~
END

ACTION_IF (GAME_IS ~bgt~) BEGIN
PRINT ~Bgt Portraits~
PRINT ~   ~
  COPY ~ChaosKnight/BMP/zychaosL.bmp~ ~override~
  COPY ~ChaosKnight/BMP/zychaosS.bmp~ ~override~
  COPY ~ChaosKnight/BMP/zychaosM.bmp~ ~override~
END

ACTION_IF (GAME_IS ~bgee eet bgt~) BEGIN

COMPILE ~ChaosKnight/D/ZYJCOB.d~

COMPILE ~ChaosKnight/D/ZYJCOBJ.d~

COMPILE ~ChaosKnight/D/ZYJCOBP.d~

COMPILE ~ChaosKnight/BAF/ZYJCOB.baf~

EXTEND_TOP ~%Beregost_BCS%.bcs~ ~ChaosKnight/BAF/AR6700.baf~
    EVALUATE_BUFFER
END

ACTION_IF (GAME_IS ~eet~) BEGIN
PRINT ~              ~
	PRINT ~APPEND pdialog.2da for EET~
PRINT ~              ~
  APPEND ~PDIALOG.2DA~
~ZYJCOB       ZYJCOBP             ZYJCOBJ            ***            ***                ***                ***                 ***~
    UNLESS ~ZYJCOB~
END ELSE ACTION_IF (GAME_IS ~bgee bgt~) BEGIN
PRINT ~              ~
	PRINT ~APPEND pdialog.2da for BGEE or BGT~
PRINT ~              ~
  APPEND ~pdialog.2da~ 
~ZYJCOB       ZYJCOBP             ZYJCOBJ            ***~
    UNLESS ~ZYJCOB~
END

LAF ADD_KIT_EX
INT_VAR
  kit_class = 2
  lower     = RESOLVE_STR_REF(@1)
  mixed     = RESOLVE_STR_REF(@2)
  help      = RESOLVE_STR_REF(@3)
  briefdesc = RESOLVE_STR_REF(@3)
STR_VAR
  kit_name  = ~CHAOSK~
  unusable  = ~0x04000000~ //kitlist.2da의 unusable 과 class 영역, 이건 새로 만들수없고 여기 있는것 중에서 골라야오류가 안난답니다. 키트별로 아머착용제한 설정 // unusable kit_class
  hpclass   = ~HPWAR~
  //biography = ....... // (optional, EE-only) The biography strref of your kit, added to clastext.2da.
  clasweap  = ~1 1 1 0 0 1 1 1~ //clasweap 8가지무기타입 사용가능설정. // clasweap
  weapprof  = ~2 2 0 2 5 5 2 0 2 2 2 2 2 2 2 2 2 5 2 2 5 5 2 0 0 0 0 0 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~ //WEAPPROF 무기 숙련도 설정.
  abclasrq  = ~9 3 9 3 3 3~ //최소 능력치 str dex con int wis chr. // abclasrq
  abclsmod  = ~0 -2 0 -2 0 0~ // 능력치 변동. // abclsmod
  abdcdsrq  = ~9 3 9 3 3 3~ //이 키트로 듀얼하기 위한 최소치. // abdcdsrq
  abdcscrq  = ~9 3 9 3 3 3~ //이 키트에서 듀얼하기 위한 최소치. // abdcscrq
  alignmnt  = ~0 0 0 0 0 0 1 1 1~ // L_G L_N L_E N_G N_N N_E C_G C_N C_E 사용가치관 설정. // alignmnt
  dualclas  = ~0 0 0 0 0 0~ // 듀얼 클래스 가능 여부설정. 순서대로, 파이터, 클레릭, 메이지, 시프, 드루이드, 레인저임. // dualclas
  clab_path = EVAL ~ChaosKnight/2DA/clabjc01.2da~ //키트어빌러티에 관련됨. 레벨업할때마다 생기는 고유능력을 적용함. 이건 새로 만드는게 좋겠죠. // clab_path
  kittable  = ~K_F_H K_F_HE~ //키트 가능 클레스와 종족 설정. // kittable  
  luabbr    = ~Fi0~ //파이터의 하이어빌사용. 대게 lu로 시작하는 테이블에 적혀있음. // luabbr
  stweap    = ~PLAT12 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL21 BRAC16 BELT06 * * * POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 XBOW06~ // 25STWEAP.2da참고
  //bdstweap  = ~PLAT12 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL21 BRAC16 BELT06 * * * POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 XBOW06~
RET
  kit_id
END

ACTION_IF (GAME_IS ~bgee eet~) BEGIN

COPY ~ChaosKnight/CRE/ZYJoJac.wav~ ~override~

END

ACTION_IF (GAME_IS ~bgee eet bgt~) BEGIN

COPY ~ChaosKnight/CRE/zychaos.cre~ ~override~
  SAY NAME1 @4
  SAY NAME2 @4
  SAY HAPPY @100	// 0xac
  SAY UNHAPPY_ANNOYED @101	// 0xb0
  SAY UNHAPPY_SERIOUS @102	// 0xb4
  SAY UNHAPPY_BREAKING_POINT @103	// 0xb8
  SAY TIRED @104	// 0xc0
  SAY BATTLE_CRY1 @105	// 0xc8
  SAY BATTLE_CRY2 @106	// 0xcc
  SAY BATTLE_CRY3 @107	// 0xd0
  SAY DYING @108	// 0xf0
  SAY HURT @109	// 0xf4
  SAY SELECT_COMMON1 @110	// 0x10c
  SAY SELECT_COMMON2 @111	// 0x110
  SAY SELECT_COMMON3 @112	// 0x114
  SAY SELECT_COMMON4 @113	// 0x118
  SAY SELECT_COMMON5 @114	// 0x11c
  SAY SELECT_COMMON6 @115	// 0x120
  SAY SELECT_ACTION1 @116	// 0x124
  SAY SELECT_ACTION2 @117	// 0x128
  SAY SELECT_ACTION3 @118	// 0x12c
  WRITE_LONG SELECT_ACTION4 (BNOT 0x0)
  WRITE_LONG SELECT_ACTION5 (BNOT 0x0)
  WRITE_LONG SELECT_ACTION6 (BNOT 0x0)
  WRITE_LONG SELECT_ACTION7 (BNOT 0x0)
  WRITE_LONG TARGET_IMMUNE (BNOT 0x0)
  WRITE_LONG SPELL_DISRUPTED (BNOT 0x0)
//  SELECT_ACTION4/5/6/7 need special handling for BGEE !!! Other examples in Setup-Ishlilkamod.TP2 and setup-BG1Aerie.tp2
  
  PATCH_IF (GAME_IS ~bgee~) BEGIN
  SAY 0x1E0 @119 // BGEE_SELECT_ACTION4 0x1E0
END ELSE BEGIN
  SAY 0x130 @119 // SELECT_ACTION4
END

//  IWDEE_SELECT_COMMON7 has issues so !!! (probably useless in this case)
  
  PATCH_IF (GAME_IS ~bgee eet~) BEGIN
  WRITE_LONG 0x204 (BNOT 0x0)  // IWDEE_SELECT_COMMON7
END

  SAY BIO @5	// 0x1cc
  ADD_CRE_ITEM ~CHAN02~ #0 #0 #0 ~UNSTEALABLE~ ~ARMOR~
  ADD_CRE_ITEM ~ZYCHMACE~ #0 #0 #0 ~UNSTEALABLE~ ~WEAPON1~ EQUIP
  WRITE_SHORT 0x244 0 // doesn't change
  
/* 
  WRITE_SHORT 0x244 0 // doesn't change
  WRITE_SHORT 0x246 0x4000 + ~%CHAOSK%~ // the internal name of your kit, surrounded by %*/

// Borrowed from Charlatan Kit by Raduziel

COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS ~9~ "rows"
  FOR ( index = 31 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 5 9 "clab"
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "CHAOSK" = 0) BEGIN
      READ_2DA_ENTRY "%index%" 0 9 "CHAOSK"
      SET "rows" = 0
    END
  END
  BUT_ONLY_IF_IT_CHANGES

//OUTER_SET kit = "%CHAOSK%"
ACTION_FOR_EACH creature IN zychaos BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%creature%.cre~ THEN BEGIN
PRINT ~Zychaos Exists~
    COPY_EXISTING ~%creature%.cre~ ~override~
      //WRITE_BYTE 0x246 kit_id
	  WRITE_SHORT 0x246 0x4000 + kit_id
      BUT_ONLY

	END
  END
 END



