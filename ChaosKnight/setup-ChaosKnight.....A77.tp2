BACKUP ~ChaosKnight/backup~
AUTHOR ~drizztdd@naver.com~
README ~Askaria/Readmes/Readme.%LANGUAGE%.txt~ ~Askaria/Readmes/readme.English.txt~

VERSION ~v2.0~

ALWAYS

ACTION_IF (GAME_IS ~bgee eet bgt~) BEGIN
INCLUDE ~ChaosKnight/Lib/g3_cpmvars_master.tpa~
END

INCLUDE ~ChaosKnight/lib/a7#add_kit_ex.tpa~

ACTION_IF (GAME_IS ~bg2ee~) BEGIN
PRINT @201 /*Only the ChaosKit will be installed on BG2EE*/
END

    //Copies tra files into the autotra-folder (to leave the originals untouched)
    DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
	  COPY ~ChaosKnight/Language/english/D~  ~ChaosKnight/Language/autotra/%LANGUAGE%/D~
      COPY ~ChaosKnight/Language/english~  ~ChaosKnight/Language/autotra/%LANGUAGE%~
	  COPY ~ChaosKnight/Language/%LANGUAGE%/D~  ~ChaosKnight/Language/autotra/%LANGUAGE%/D~
      COPY ~ChaosKnight/Language/%LANGUAGE%~  ~ChaosKnight/Language/autotra/%LANGUAGE%~
    END

    LAF autotra_workaround END


//  ACTION_DEFINE_ARRAY fl#noconvert BEGIN END

 ACTION_DEFINE_ARRAY fl#reload BEGIN setup END

    LAF HANDLE_CHARSETS
      INT_VAR
        infer_charsets = 1
      STR_VAR
        tra_path = EVAL ~ChaosKnight/Language/autotra~
//        noconvert_array = fl#noconvert
        reload_array = fl#reload
 END
END

AUTO_TRA ~ChaosKnight/Language/autotra/%LANGUAGE%/D~

LANGUAGE ~한국어~ ~korean~ ~ChaosKnight/Language/korean/setup.tra~
LANGUAGE ~English~ ~english~ ~ChaosKnight/Language/english/setup.tra~

BEGIN @0
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet bgt~) @200 /*ChaosKnight must be installed on BGT, BGEE, BG2EE or EET*/

COPY ~ChaosKnight/SPL/zycs1.spl~ ~override~
   SAY NAME1 @8

COPY ~ChaosKnight/SPL/zycs2.spl~ ~override~
   SAY NAME1 @9

COPY ~ChaosKnight/SPL/zycs3.spl~ ~override~
   SAY NAME1 @10

COPY ~ChaosKnight/SPL/zycs4.spl~ ~override~
   SAY NAME1 @11

COPY ~ChaosKnight/SPL/zycs5.spl~ ~override~
   SAY NAME1 @12

COPY ~ChaosKnight/SPL/zycs6.spl~ ~override~
   SAY NAME1 @13

COPY ~ChaosKnight/SPL/zycs7.spl~ ~override~
   SAY NAME1 @14

COPY ~ChaosKnight/SPL/zycs8.spl~ ~override~
   SAY NAME1 @15

COPY ~ChaosKnight/SPL/zycs8_1.spl~ ~override~

COPY ~ChaosKnight/EFF/zychaosd.eff~ ~override~

COPY ~ChaosKnight/EFF/zychaost.eff~ ~override~

ACTION_IF (GAME_IS ~bgee eet~) BEGIN
PRINT ~EE item~
COPY ~ChaosKnight/ITM/zychmace.itm~ ~override~
  SAY NAME1 @18
  SAY NAME2 @6
  SAY UNIDENTIFIED_DESC @16 
  SAY DESC @17
END

ACTION_IF (GAME_IS ~bgt~) BEGIN
PRINT ~Bgt item~
COPY ~ChaosKnight/ITM/zychmace.itm~ ~override~
  SAY NAME1 @18
  SAY NAME2 @6
  SAY UNIDENTIFIED_DESC @16 
  SAY DESC @7
END

// Portraits

ACTION_IF (GAME_IS ~bgee eet~) BEGIN
PRINT ~EE Portraits~
  COPY ~ChaosKnight/BMP/EE/zychaosL.bmp~ ~override~
  COPY ~ChaosKnight/BMP/EE/zychaosS.bmp~ ~override~
  COPY ~ChaosKnight/BMP/EE/zychaosM.bmp~ ~override~
END

ACTION_IF (GAME_IS ~bgt~) BEGIN
PRINT ~Bgt Portraits~
  COPY ~ChaosKnight/BMP/zychaosL.bmp~ ~override~
  COPY ~ChaosKnight/BMP/zychaosS.bmp~ ~override~
  COPY ~ChaosKnight/BMP/zychaosM.bmp~ ~override~
END

ACTION_IF (GAME_IS ~bgee eet bgt~) BEGIN

COMPILE ~ChaosKnight/D/ZYJCOB.d~

COMPILE ~ChaosKnight/D/ZYJCOBJ.d~

COMPILE ~ChaosKnight/D/ZYJCOBP.d~

COMPILE ~ChaosKnight/BAF/ZYJCOB.baf~

EXTEND_TOP ~%Beregost_BCS%.bcs~ ~ChaosKnight/BAF/AR6700.baf~
    EVALUATE_BUFFER
END

ACTION_IF (GAME_IS ~eet~) BEGIN
  APPEND ~PDIALOG.2DA~
~ZYJCOB       ZYJCOBP             ZYJCOBJ            ***            ***                ***                ***                 ***~
    UNLESS ~ZYJCOB~
END

ACTION_IF (GAME_IS ~bgee bgt~) BEGIN
  APPEND ~pdialog.2da~ 
~ZYJCOB       ZYJCOBP             ZYJCOBJ            ***~
    UNLESS ~ZYJCOB~
END

LAF ADD_KIT_EX
INT_VAR
  kit_class = 2
  lower     = RESOLVE_STR_REF(@1)
  mixed     = RESOLVE_STR_REF(@2)
  help      = RESOLVE_STR_REF(@3)
  briefdesc = RESOLVE_STR_REF(@3)
STR_VAR
  kit_name  = ~CHAOSK~
  unusable  = ~0x04000000~ //kitlist.2da의 unusable 과 class 영역, 이건 새로 만들수없고 여기 있는것 중에서 골라야오류가 안난답니다. 키트별로 아머착용제한 설정 // unusable kit_class
  hpclass   = ~HPWAR~
  //biography = ....... // (optional, EE-only) The biography strref of your kit, added to clastext.2da.
  clasweap  = ~1 1 1 0 0 1 1 1~ //clasweap 8가지무기타입 사용가능설정. // clasweap
  weapprof  = ~2 2 0 2 5 5 2 0 2 2 2 2 2 2 2 2 2 5 2 2 5 5 2 0 0 0 0 0 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~ //WEAPPROF 무기 숙련도 설정.
  abclasrq  = ~9 3 9 3 3 3~ //최소 능력치 str dex con int wis chr. // abclasrq
  abclsmod  = ~0 -2 0 -2 0 0~ // 능력치 변동. // abclsmod
  abdcdsrq  = ~9 3 9 3 3 3~ //이 키트로 듀얼하기 위한 최소치. // abdcdsrq
  abdcscrq  = ~9 3 9 3 3 3~ //이 키트에서 듀얼하기 위한 최소치. // abdcscrq
  alignmnt  = ~0 0 0 0 0 0 1 1 1~ // L_G L_N L_E N_G N_N N_E C_G C_N C_E 사용가치관 설정. // alignmnt
  dualclas  = ~0 0 0 0 0 0~ // 듀얼 클래스 가능 여부설정. 순서대로, 파이터, 클레릭, 메이지, 시프, 드루이드, 레인저임. // dualclas
  clab_path = EVAL ~ChaosKnight/2DA/clabjc01.2da~ //키트어빌러티에 관련됨. 레벨업할때마다 생기는 고유능력을 적용함. 이건 새로 만드는게 좋겠죠. // clab_path
  kittable  = ~K_F_H K_F_HE~ //키트 가능 클레스와 종족 설정. // kittable  
  luabbr    = ~Fi0~ //파이터의 하이어빌사용. 대게 lu로 시작하는 테이블에 적혀있음. // luabbr
  stweap    = ~PLAT12 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL21 BRAC16 BELT06 * * * POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 XBOW06~ // 25STWEAP.2da참고
  //bdstweap  = ~PLAT12 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL21 BRAC16 BELT06 * * * POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 XBOW06~
RET
  kit_id
END

ACTION_IF (GAME_IS ~bgee eet bgt~) BEGIN

COPY ~ChaosKnight/CRE/zychaos.cre~ ~override~
  SAY 0xac  @100	//HAPPY
  SAY 0xb0  @101	//UNHAPPY_ANNOYED
  SAY 0xb4  @102	//UNHAPPY_SERIOUS
  SAY 0xb8  @103	//UNHAPPY_BREAKING_POINT
  SAY 0xc0  @104	//TIRED
  SAY 0xc8  @105	//BATTLE_CRY1
  SAY 0xcc  @106	//BATTLE_CRY2	
  SAY 0xd0  @107	//BATTLE_CRY3
  SAY 0xf0  @108	//DYING
  SAY 0xf4  @109	//HURT
  SAY 0x10c @110	//SELECT_COMMON1
  SAY 0x110 @111	//SELECT_COMMON2
  SAY 0x114 @112	//SELECT_COMMON3
  SAY 0x118 @113	//SELECT_COMMON4
  SAY 0x11c @114	//SELECT_COMMON5
  SAY 0x120 @115	//SELECT_COMMON6
  SAY 0x124 @116	//SELECT_ACTION1
  SAY 0x128 @117	//SELECT_ACTION2
  SAY 0x12c @118	//SELECT_ACTION3
  SAY 0x130 @119	//SELECT_ACTION4
  SAY NAME1 @4
  SAY NAME2 @4
  SAY 0x1cc @5	//BIO
  ADD_CRE_ITEM ~CHAN02~ #0 #0 #0 ~UNSTEALABLE~ ~ARMOR~
  ADD_CRE_ITEM ~ZYCHMACE~ #0 #0 #0 ~UNSTEALABLE~ ~WEAPON1~ EQUIP
  WRITE_SHORT 0x244 0 // doesn't change
  
/* 
  WRITE_SHORT 0x244 0 // doesn't change
  WRITE_SHORT 0x246 0x4000 + ~%CHAOSK%~ // the internal name of your kit, surrounded by %*/

// Borrowed from Charlatan Kit by Raduziel

COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS ~9~ "rows"
  FOR ( index = 31 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 5 9 "clab"
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "CHAOSK" = 0) BEGIN
      READ_2DA_ENTRY "%index%" 0 9 "CHAOSK"
      SET "rows" = 0
    END
  END
  BUT_ONLY_IF_IT_CHANGES

OUTER_SET kit = "%CHAOSK%"
ACTION_FOR_EACH creature IN zychaos BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%creature%.cre~ THEN BEGIN
PRINT ~Zychaos Exists~
    COPY_EXISTING ~%creature%.cre~ ~override~
      WRITE_SHORT   0x244 0
      WRITE_BYTE    0x246 "%kit%"
      WRITE_BYTE    0x247 0x40
      BUT_ONLY

	END
  END
 END



